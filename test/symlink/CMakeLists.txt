set(test_symlink_dir ${CMAKE_CURRENT_BINARY_DIR}/symlink_test_dir)
file(MAKE_DIRECTORY ${test_symlink_dir})
set(_s_file cmake_test_symlink.txt)
file(GENERATE OUTPUT ${test_symlink_dir}/${_s_file} CONTENT "I was generated by CMake")

set(test_symlink_dir2 ${test_symlink_dir}/link.dir)
file(CREATE_LINK ${test_symlink_dir} ${test_symlink_dir2} SYMBOLIC)

set(test_symlink_file ${test_symlink_dir2}/${_s_file}.link)
file(CREATE_LINK ${test_symlink_dir2}/${_s_file} ${test_symlink_file} SYMBOLIC)
message(VERBOSE "CMake test link file ${test_symlink_file}")


add_executable(test_is_symlink_cpp test_is_symlink.cpp)
add_test(NAME Cpp_is_symlink COMMAND test_is_symlink_cpp ${test_symlink_file} ${test_symlink_dir2})
set_tests_properties(Cpp_is_symlink PROPERTIES FIXTURES_SETUP is_symlink)

add_executable(test_symlink_cpp test_symlink.cpp)
target_include_directories(test_symlink_cpp PRIVATE ..)

add_test(NAME Cpp_symlink COMMAND test_symlink_cpp)
set_tests_properties(Cpp_symlink PROPERTIES
FIXTURES_SETUP symlink
FIXTURES_REQUIRED is_symlink
WORKING_DIRECTORY $<TARGET_FILE_DIR:test_symlink_cpp>
)

foreach(t IN ITEMS is_symlink_cpp symlink_cpp)
  target_include_directories(test_${t} PRIVATE ..)
  target_link_libraries(test_${t} PRIVATE ffilesystem)
endforeach()


if(HAVE_Fortran_FILESYSTEM)

add_executable(test_is_symlink_fortran test_is_symlink.f90)
add_test(NAME Fortran_is_symlink COMMAND test_is_symlink_fortran ${test_symlink_file})
set_tests_properties(Fortran_is_symlink PROPERTIES FIXTURES_SETUP is_symlink)

add_executable(test_symlink_fortran test_symlink.f90)
add_test(NAME Fortran_symlink COMMAND test_symlink_fortran)
set_tests_properties(Fortran_symlink PROPERTIES
FIXTURES_SETUP symlink
FIXTURES_REQUIRED is_symlink
SKIP_RETURN_CODE 77
)

foreach(t IN ITEMS is_symlink_fortran symlink_fortran)
  target_link_libraries(test_${t} PRIVATE ffilesystem)
  target_compile_options(test_${t} PRIVATE ${ffilesystem_fortran_test_flags})
  set_property(TARGET test_${t} PROPERTY LINKER_LANGUAGE Fortran)
endforeach()

endif()
